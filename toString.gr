@noPervasives
module ToString

from "runtime/unsafe/wasmi32" include WasmI32
from "runtime/unsafe/wasmi64" include WasmI64
from "runtime/unsafe/wasmf32" include WasmF32
from "runtime/unsafe/wasmf64" include WasmF64
from "runtime/unsafe/memory" include Memory
from "runtime/unsafe/offsets" include Offsets
from "runtime/bigint" include Bigint
from "runtime/numberUtils" include NumberUtils
from "runtime/numbers" include Numbers
from "runtime/dataStructures" include DataStructures
use DataStructures.{
  allocateString,
  allocateArray,
  // Tagging
  untagSimpleNumber,
  // Short Values
  untagChar,
  untagInt8,
  untagInt16,
  untagUint8,
  untagUint16,
}
from "runtime/utf8" include Utf8
use Utf8.{ usvEncodeLength, writeUtf8CodePoint }
from "./grainValue.gr" include GrainValue
from "./doc.gr" include Doc

primitive ignore = "@ignore"
primitive (!) = "@not"
primitive arrayLength = "@array.length"
primitive magic = "@magic"

// Specific stringification functions for various data types
module Stringify {
  // Settings
  @unsafe
  let defaultRadix = 10n // The default radix for numbers
  // TODO: Maybe play around with experimental color and indentation setting support????
  abstract record StringifyState<a> {
    topLevel: Bool,
    cycleStack: List<GrainValue.HeapValue<a>>,
  }
  provide let emptyState = { topLevel: true, cycleStack: [] }

  // Heap Values
  @unsafe
  provide let string = (val: String) => Doc.string(val) // TODO: Escaping
  @unsafe
  provide let bytes = (val: Bytes) => {
    // TODO: Is there a slightly safer way of doing this?
    use Doc.{ (++) }
    use WasmI32.{ (+), (-), (*), (<), (>), (==) }
    let ptr = WasmI32.fromGrain(val)
    let len = WasmI32.load(ptr, Offsets._BYTES_LEN_OFFSET)
    let len = if (len > 32n) 32n else len // Limit to 32 bytes for display

    let hex = NumberUtils.get_HEX_DIGITS()
    let strLen = len * 3n - 1n // 2 hex digits + 1 space per byte
    let str = allocateString(strLen)
    let bytesOffset = ptr + Offsets._BYTES_DATA_OFFSET
    let strOffset = str + Offsets._STR_DATA_OFFSET
    Memory.fill(strOffset, 0x20n, strLen) // Fill with spaces
    for (let mut i = 0n; i < len; i += 1n) {
      let n = WasmI32.load8U(bytesOffset, i)
      let j = i * 3n
      let k = n * 2n
      WasmI32.store16(strOffset, WasmI32.load16U(hex, k), j)
    }
    let content = Doc.string(WasmI32.toGrain(str): String)
    ignore(val)
    let content = if (len < 32n) content else content ++ Doc.string("...")
    Doc.angleBrackets(Doc.string("bytes: ") ++ content)
  }
  @unsafe
  provide let number = (val: Number) => {
    match (GrainValue.getNumberTag(val)) {
      GrainValue.SimpleNumberTag(v) =>
        Doc.string(NumberUtils.itoa32(untagSimpleNumber(v), defaultRadix)),
      GrainValue.Int64Tag(v) => {
        let ptr = WasmI32.fromGrain(v)
        let num = WasmI64.load(ptr, Offsets._INT64_VALUE_OFFSET)
        ignore(v)
        Doc.string(NumberUtils.itoa64(num, defaultRadix))
      },
      GrainValue.Float64Tag(v) => {
        let ptr = WasmI32.fromGrain(v)
        let num = WasmF64.load(ptr, Offsets._FLOAT64_VALUE_OFFSET)
        ignore(v)
        Doc.string(NumberUtils.dtoa(num))
      },
      GrainValue.RationalTag(v) => {
        use Doc.{ (++) }
        let ptr = WasmI32.fromGrain(v)
        let numerator = Bigint.bigIntToString10(WasmI32.load(ptr, 8n)) // TODO: Magic Offset
        let denominator = Bigint.bigIntToString10(WasmI32.load(ptr, 12n)) // TODO: Magic Offset
        ignore(v)
        Doc.string(numerator) ++ Doc.string("/") ++ Doc.string(denominator)
      },
      GrainValue.BigIntTag(v) => {
        let ptr = WasmI32.fromGrain(v)
        let str = Bigint.bigIntToString10(ptr)
        ignore(v)
        Doc.string(str)
      },
      GrainValue.UnknownNumberTag => Doc.string("<unknown boxed number>"),
    }
  }
  @unsafe
  provide let int32 = (val: Int32) => {
    let ptr = WasmI32.fromGrain(val)
    let num = WasmI32.load(ptr, Offsets._INT32_VALUE_OFFSET)
    ignore(val)
    Doc.string(NumberUtils.itoa32(num, defaultRadix))
  }
  @unsafe
  provide let float32 = (val: Float32) => {
    let ptr = WasmI32.fromGrain(val)
    let num = WasmF64.promoteF32(
      WasmF32.load(ptr, Offsets._FLOAT32_VALUE_OFFSET)
    )
    ignore(val)
    Doc.string(NumberUtils.dtoa(num))
  }
  @unsafe
  provide let uint32 = (val: Uint32) => {
    let ptr = WasmI32.fromGrain(val)
    let num = WasmI32.load(ptr, Offsets._UINT32_VALUE_OFFSET)
    ignore(val)
    Doc.string(NumberUtils.utoa32(num, defaultRadix))
  }
  @unsafe
  provide let uint64 = (val: Uint64) => {
    let ptr = WasmI32.fromGrain(val)
    let num = WasmI64.load(ptr, Offsets._UINT64_VALUE_OFFSET)
    ignore(val)
    Doc.string(NumberUtils.utoa64(num, defaultRadix))
  }
  // Short Values
  @unsafe
  provide let char = (val: Char, state) => {
    use Doc.{ (++) }
    // TODO: It would be nice if we could share this logic with strings
    let content = match (val) {
      '\b' when !state.topLevel => "\\b",
      '\f' when !state.topLevel => "\\f",
      '\n' when !state.topLevel => "\\n",
      '\r' when !state.topLevel => "\\r",
      '\t' when !state.topLevel => "\\t",
      '\v' when !state.topLevel => "\\v",
      '\\' when !state.topLevel => "\\\\",
      '\'' when !state.topLevel => "\\'",
      _ => {
        use WasmI32.{ (+) }
        let usv = untagChar(val)
        let byteCount = usvEncodeLength(usv)
        let string = allocateString(byteCount)
        writeUtf8CodePoint(string + Offsets._STR_DATA_OFFSET, usv)
        WasmI32.toGrain(string): String
      },
    }
    if (state.topLevel) {
      Doc.string(content)
    } else {
      Doc.singleQuotes(Doc.string(content))
    }
  }
  @unsafe
  provide let int8 = (val: Int8) =>
    Doc.string(NumberUtils.itoa32(untagInt8(val), defaultRadix))
  @unsafe
  provide let int16 = (val: Int16) =>
    Doc.string(NumberUtils.itoa32(untagInt16(val), defaultRadix))
  @unsafe
  provide let uint8 = (val: Uint8) =>
    Doc.string(NumberUtils.itoa32(untagUint8(val), defaultRadix))
  @unsafe
  provide let uint16 = (val: Uint16) =>
    Doc.string(NumberUtils.itoa32(untagUint16(val), defaultRadix))

  // Polymorphic conversions
  let shortValueToString = (val: GrainValue.ShortValue<a>, state) => {
    // Get The Short Tag
    match (GrainValue.getShortTag(val)) {
      CharTag(v) => char(v, state),
      Int8Tag(v) => int8(v),
      Int16Tag(v) => int16(v),
      Uint8Tag(v) => uint8(v),
      Uint16Tag(v) => uint16(v),
      UnknownShortTag => Doc.string("<unknown short value>"),
    }
  }

  // Constant Values
  @unsafe
  let constantValueToString = (val: GrainValue.ConstantValue<a>) => {
    use WasmI32.{ (==) }
    match (WasmI32.fromGrain(val)) {
      v when v == WasmI32.fromGrain(true) => Doc.string("true"),
      v when v == WasmI32.fromGrain(false) => Doc.string("false"),
      v when v == WasmI32.fromGrain(void) => Doc.string("void"),
      _ => Doc.string("<unknown small value>"),
    }
  }

  @unsafe
  let rec heapValueToString = (val: GrainValue.HeapValue<b>, state) => {
    // TODO: Handle Cycle - Check the stack for cycles emit <Cycle $n>
    // Get The Heap Tag
    match (GrainValue.getHeapTag(val)) {
      TupleTag(v) =>
        tuple(v, { ...state, cycleStack: [val, ...state.cycleStack] }),
      ArrayTag(v) =>
        array(v, { ...state, cycleStack: [val, ...state.cycleStack] }),
      RecordTag(v) =>
        _record(v, { ...state, cycleStack: [val, ...state.cycleStack] }),
      ADTTag(v) => adt(v, { ...state, cycleStack: [val, ...state.cycleStack] }),
      ClosureTag(_) => Doc.string("<lambda>"),
      StringTag(v) => string(v),
      BytesTag(v) => bytes(v),
      // Boxed Number Types
      BoxedNumberTag(v) => number(v),
      // Generic Number Types
      Int32Tag(v) => int32(v),
      Float32Tag(v) => float32(v),
      Uint32Tag(v) => uint32(v),
      Uint64Tag(v) => uint64(v),
      // Unknown
      GrainValue.UnknownTag => Doc.string("<unknown heap value>"),
    }
  }
  and tupleHelp = (vals: List<GrainValue.StackTag<f>>, isVariant, state) => {
    let state = { ...state, topLevel: false }
    use Doc.{ (++) }
    let content = Doc.parens(
      Doc.indent(
        Doc.concatMap(
          lead=next => Doc._break,
          sep=(prev, next) => Doc.breakableSpace,
          trail=prev => Doc.empty,
          f=(final, val) => {
            let val = taggedValue(val, state)
            if (final) {
              Doc.group(val) ++ Doc.trailingComma
            } else {
              Doc.group(val ++ Doc.comma)
            }
          },
          vals
        )
      )
        ++ Doc._break
    )
    match (vals) {
      [_] when !isVariant => Doc.string("box") ++ content,
      _ => content,
    }
  }
  and tuple = (val: GrainValue.TupleValue<e>, state) => {
    let vals = GrainValue.getTupleData(val)
    tupleHelp(vals, false, state)
  }
  and array = (val: Array<a>, state) => {
    let state = { ...state, topLevel: false }
    use Doc.{ (++) }
    use Numbers.{ (>=), (-) }
    let mut vals = []
    for (let mut i = arrayLength(val) - 1; i >= 0; i -= 1) {
      // NOTE: We use `magic` here to prevent unifying the param
      vals = [value(magic(val[i]): c, state), ...vals]
    }
    Doc.arrayBrackets(
      Doc.indent(
        Doc.concatMap(
          lead=next => Doc._break,
          sep=(prev, next) => Doc.breakableSpace,
          trail=prev => Doc.empty,
          f=(final, val) => if (final) {
            Doc.group(val) ++ Doc.trailingComma
          } else {
            Doc.group(val ++ Doc.comma)
          },
          vals
        )
      )
        ++ Doc._break
    )
  }
  and _recordHelp = (vals: List<(String, GrainValue.StackTag<f>)>, state) => {
    use Doc.{ (++) }
    Doc.braces(
      Doc.indent(
        Doc.concatMap(
          lead=next => Doc.space ++ Doc._break,
          sep=(prev, next) => Doc.breakableSpace,
          trail=last => Doc.space,
          f=(final, (name, val)) => {
            let val = taggedValue(val, state)
            let entry = Doc.group(
              Doc.string(name) ++ Doc.string(":") ++ Doc.space ++ val
            )
            if (final) {
              entry ++ match (vals) {
                [vals] => Doc.comma,
                _ => Doc.trailingComma,
              }
            } else {
              Doc.group(entry ++ Doc.comma)
            }
          },
          vals
        )
      )
        ++ Doc._break
    )
  }
  and _record = (val: GrainValue.RecordValue<a>, state) => {
    let vals = GrainValue.getRecordData(val)
    match (vals) {
      Some(vals) => _recordHelp(vals, state),
      None => Doc.string("<record value>"),
    }
  }
  and adt = (val: GrainValue.ADTValue<a>, state) => {
    use Doc.{ (++) }
    let (name, variantData) = GrainValue.getADTData(val)
    match (name) {
      Some(name) => Doc.string(name),
      None => Doc.string("<enum value>"),
    } ++ match (variantData) {
      GrainValue.TupleVariant(vals) =>
        tupleHelp(vals, true, { ...state, topLevel: false }),
      GrainValue.RecordVariant(vals) =>
        _recordHelp(vals, { ...state, topLevel: false }),
      EmptyVariant => Doc.empty,
    }
  }
  and taggedValue = (val: GrainValue.StackTag<d>, state) => {
    match (val) {
      HeapTag(v) => heapValueToString(v, state),
      ShortTag(v) => shortValueToString(v, state),
      NumberTag(v) => number(v),
      ConstantTag(v) => constantValueToString(v),
      ReservedTag(_) => Doc.string("<unknown value>"),
    }
  }
  and value = (val: a, state) => {
    match (GrainValue.getTag(val)) {
      HeapTag(v) => heapValueToString(v, state),
      ShortTag(v) => shortValueToString(v, state),
      NumberTag(v) => number(v),
      ConstantTag(v) => constantValueToString(v),
      ReservedTag(_) => Doc.string("<unknown value>"),
    }
  }

  provide { value }
}

provide let toString = (val: a) =>
  Doc.Engine.toString(LF, 80l, Stringify.value(val, Stringify.emptyState))
