/*
 * toString.gr
 * Copyright (c) 2025 Jake Follest
 *
 * This file uses code from `libs/doc.gr`, which is licensed under the GPL.
 * Your use of this file is therefore subject to the GPL license.
 *
 * Original contributions by Jake Follest are MIT licensed, but redistribution
 * must comply with the GPL where the GPL code is used.
 */

/**
 * Module for converting Grain values to their string representations.
 *
 * @example toString(true) == "true"
 * @example toString(123) == "123"
 */
@noPervasives
module ToString

from "runtime/unsafe/wasmi32" include WasmI32
from "runtime/unsafe/wasmf64" include WasmF64
from "runtime/unsafe/memory" include Memory
from "runtime/unsafe/offsets" include Offsets
use Offsets.{
  _BYTES_LEN_OFFSET,
  _BYTES_DATA_OFFSET,
  _STR_LEN_OFFSET,
  _STR_DATA_OFFSET,
}
from "runtime/bigint" include Bigint
from "runtime/numberUtils" include NumberUtils
from "runtime/dataStructures" include DataStructures
use DataStructures.{
  allocateString,
  // Tagging
  tagSimpleNumber,
  untagSimpleNumber,
  // Short Values
  untagChar,
  untagInt8,
  untagInt16,
  untagUint8,
  untagUint16,
}
from "runtime/utf8" include Utf8
use Utf8.{ usvEncodeLength, writeUtf8CodePoint }
from "./libs/grainValue.gr" include GrainValue
from "./libs/doc.gr" include Doc

@unsafe
let _RATIONAL_NUMERATOR_OFFSET = 8n
@unsafe
let _RATIONAL_DENOMINATOR_OFFSET = 12n

primitive ignore = "@ignore"
primitive (!) = "@not"
primitive (&&) = "@and"
primitive (is) = "@is"
primitive magic = "@magic"
primitive box = "@box"
primitive unbox = "@unbox"

// Computes the escaped length of a usv
@unsafe
let computeEscapedUSVLength = (usv: WasmI32, isString: Bool, topLevel: Bool) => {
  match (usv) {
    0x08n | 0x0Cn | 0x0An | 0x0Dn | 0x09n | 0x0Bn | 0x5Cn when !topLevel => 2n,
    0x27n when !isString && !topLevel => 2n,
    0x22n when isString && !topLevel => 2n,
    _ => usvEncodeLength(usv),
  }
}

// Writes an escaped usv to the given pointer
@unsafe
let writeEscapedUSV = (
  usv: WasmI32,
  ptr: WasmI32,
  isString: Bool,
  topLevel: Bool,
) => {
  use WasmI32.{ (+), (==) }
  let mut ptr = ptr
  if (!topLevel) {
    if (computeEscapedUSVLength(usv, isString, topLevel) == 2n) {
      ptr += writeUtf8CodePoint(ptr, 0x5Cn) // \
    }
    ptr += writeUtf8CodePoint(ptr, match (usv) {
      0x08n => 0x62n, // \b,
      0x0Cn => 0x66n, // \f
      0x0An => 0x6En, // \n
      0x0Dn => 0x72n, // \r
      0x09n => 0x74n, // \t
      0x0Bn => 0x76n, // \v
      0x5Cn => 0x5Cn, // \\
      0x27n when !isString => 0x27n, // \'
      0x22n when isString => 0x22n, // \"
      _ => usv,
    })
  } else {
    ptr += writeUtf8CodePoint(ptr, usv)
  }
  ptr
}

// Determines if a heap value is in the current cycle stack
@unsafe
let rec isCycleInStack = (
  value: GrainValue.HeapValue<a>,
  cycleStack: List<GrainValue.HeapValue<a>>,
) => {
  match (cycleStack) {
    [] => false,
    [stackValue, ..._] when stackValue is value => {
      true
    },
    [_, ...tail] => isCycleInStack(value, tail),
  }
}

// Determines the cycle number of a heap value
@unsafe
let rec getCycleNumber = (
  value: GrainValue.HeapValue<a>,
  cycleMap: List<(GrainValue.HeapValue<a>, Number)>,
) => {
  match (cycleMap) {
    [] => -1n,
    [(head, num), ..._] when head is value => untagSimpleNumber(num),
    [_, ...tail] => getCycleNumber(value, tail),
  }
}

/**
 * A container for the stringification logic.
 *
 * This can be exposed if we ever want to allow using the individual data type
 * stringification functions externally.
 */
module Stringify {
  // Settings
  @unsafe
  let defaultRadix = 10n // The default radix for numbers
  /**
   * State used during stringification to track cycles and context.
   */
  abstract record StringifyState<a> {
    /** Indicates if we are currently stringifying the top-level value. */
    topLevel: Bool,
    /** The current stack of heap values being stringified to detect cycles. */
    cycleStack: List<GrainValue.HeapValue<a>>,
    /** A map of heap values to their cycle numbers. */
    cycleMap: Box<List<(GrainValue.HeapValue<a>, Number)>>,
  }
  /**
   * Creates an initial empty stringification state.
   *
   * @returns An empty stringification state
   */
  provide let emptyState = () =>
    { topLevel: true, cycleStack: [], cycleMap: box([]) }

  // Heap Values
  /**
   * Converts a String value to its string representation.
   *
   * @param val: The String value to convert
   *
   * @returns A document layout representing the stringified String value
   */
  @unsafe
  provide let string = (val: String, state) => {
    use WasmI32.{ (+), (-), (>) }
    let strPointer = WasmI32.fromGrain(val)
    let strLength = WasmI32.load(strPointer, _STR_LEN_OFFSET)
    // Compute String Length with Escaping
    let mut escapedLength = 0n
    let mut currentPtr = strPointer + _STR_DATA_OFFSET
    let mut remainingByteLength = strLength
    while (remainingByteLength > 0n) {
      let usv = Utf8.getCodePoint(currentPtr)
      let usvSize = Utf8.usvEncodeLength(usv)
      remainingByteLength -= usvSize
      currentPtr += usvSize
      escapedLength += computeEscapedUSVLength(usv, true, state.topLevel)
    }
    // Write String with Escaping
    let escapedStr = allocateString(escapedLength)
    let mut currentPtr = strPointer + _STR_DATA_OFFSET
    let mut escapedPtr = escapedStr + _STR_DATA_OFFSET
    let mut remainingByteLength = strLength
    while (remainingByteLength > 0n) {
      let usv = Utf8.getCodePoint(currentPtr)
      let usvSize = Utf8.usvEncodeLength(usv)
      remainingByteLength -= usvSize
      currentPtr += usvSize
      escapedPtr = writeEscapedUSV(usv, escapedPtr, true, state.topLevel)
    }
    let content = Doc.Builder.string(WasmI32.toGrain(escapedStr): String)
    ignore(val)
    if (state.topLevel) {
      content
    } else {
      Doc.Builder.doubleQuotes(content)
    }
  }
  /**
   * Converts a Bytes value to its string representation.
   *
   * @param val: The Bytes value to convert
   *
   * @returns A document layout representing the stringified Bytes value
   */
  @unsafe
  provide let bytes = (val: Bytes) => {
    use Doc.Builder.{ (++) }
    use WasmI32.{ (+), (-), (*), (<), (>) }
    let mut ptr = WasmI32.fromGrain(val)
    let len = WasmI32.load(ptr, _BYTES_LEN_OFFSET)
    let len = if (len > 32n) 32n else len // Limit to 32 bytes for display
    let ptr = ptr + _BYTES_DATA_OFFSET

    let hex = NumberUtils.get_HEX_DIGITS()
    let strLen = len * 3n - 1n // 2 hex digits + 1 space per byte - trailing space
    let str = allocateString(strLen)
    let strOffset = str + _STR_DATA_OFFSET
    Memory.fill(strOffset, 0x20n, strLen) // Fill with spaces
    for (let mut i = 0n; i < len; i += 1n) {
      let n = WasmI32.load8U(ptr, i) * 2n
      WasmI32.store16(strOffset, WasmI32.load16U(hex, n), i * 3n)
    }
    ignore(val)
    let content = Doc.Builder.string(WasmI32.toGrain(str): String)
    let content = if (len < 32n)
      content
    else
      content ++ Doc.Builder.string("...")
    Doc.Builder.angleBrackets(Doc.Builder.string("bytes: ") ++ content)
  }
  /**
   * Converts a Number value to its string representation.
   *
   * @param val: The Number value to convert
   *
   * @returns A document layout representing the stringified Number value
   */
  @unsafe
  provide let number = (val: Number) => {
    match (GrainValue.getNumberTag(val)) {
      GrainValue.SimpleNumberTag(v) =>
        Doc.Builder.string(
          NumberUtils.itoa32(untagSimpleNumber(v), defaultRadix)
        ),
      GrainValue.Int64Tag(v) => {
        let num = GrainValue.getInt64Value(v)
        Doc.Builder.string(NumberUtils.itoa64(num, defaultRadix))
      },
      GrainValue.Float64Tag(v) => {
        let num = GrainValue.getFloat64Value(v)
        Doc.Builder.string(NumberUtils.dtoa(num))
      },
      GrainValue.RationalTag(v) => {
        use Doc.Builder.{ (++) }
        let ptr = WasmI32.fromGrain(v)
        let numerator = Bigint.bigIntToString(
          WasmI32.load(ptr, _RATIONAL_NUMERATOR_OFFSET),
          defaultRadix
        )
        let denominator = Bigint.bigIntToString(
          WasmI32.load(ptr, _RATIONAL_DENOMINATOR_OFFSET),
          defaultRadix
        )
        ignore(v)
        Doc.Builder.string(numerator)
          ++ Doc.Builder.string("/")
          ++ Doc.Builder.string(denominator)
      },
      GrainValue.BigIntTag(v) => {
        let ptr = WasmI32.fromGrain(v)
        let str = Bigint.bigIntToString(ptr, defaultRadix)
        ignore(v)
        Doc.Builder.string(str)
      },
      GrainValue.UnknownNumberTag => Doc.Builder.string("<unknown number>"),
    }
  }
  /**
   * Converts an Int32 value to its string representation.
   *
   * @param val: The Int32 value to convert
   *
   * @returns A document layout representing the stringified Int32 value
   */
  @unsafe
  provide let int32 = (val: Int32) => {
    let num = GrainValue.getInt32Value(val)
    Doc.Builder.string(NumberUtils.itoa32(num, defaultRadix))
  }
  /**
   * Converts a Float32 value to its string representation.
   *
   * @param val: The Float32 value to convert
   *
   * @returns A document layout representing the stringified Float32 value
   */
  @unsafe
  provide let float32 = (val: Float32) => {
    let num = GrainValue.getFloat32Value(val)
    let num = WasmF64.promoteF32(num)
    Doc.Builder.string(NumberUtils.dtoa(num))
  }
  /**
   * Converts a Uint32 value to its string representation.
   *
   * @param val: The Uint32 value to convert
   *
   * @returns A document layout representing the stringified Uint32 value
   */
  @unsafe
  provide let uint32 = (val: Uint32) => {
    let num = GrainValue.getUint32Value(val)
    Doc.Builder.string(NumberUtils.utoa32(num, defaultRadix))
  }
  /**
   * Converts a Uint64 value to its string representation.
   *
   * @param val: The Uint64 value to convert
   *
   * @returns A document layout representing the stringified Uint64 value
   */
  @unsafe
  provide let uint64 = (val: Uint64) => {
    let num = GrainValue.getUint64Value(val)
    Doc.Builder.string(NumberUtils.utoa64(num, defaultRadix))
  }
  // Short Values
  /**
   * Converts a Char value to its string representation.
   *
   * @param val: The Char value to convert
   *
   * @returns A document layout representing the stringified Char value
   */
  @unsafe
  provide let char = (val: Char, state) => {
    use WasmI32.{ (+) }
    let usv = untagChar(val)
    let strLength = computeEscapedUSVLength(usv, false, state.topLevel)
    let str = allocateString(strLength)
    writeEscapedUSV(usv, str + _STR_DATA_OFFSET, false, state.topLevel)
    ignore(val)
    let content = Doc.Builder.string(WasmI32.toGrain(str): String)
    if (state.topLevel) {
      content
    } else {
      Doc.Builder.singleQuotes(content)
    }
  }
  /**
   * Converts an Int8 byte value to its string representation.
   *
   * @param val: The Int8 value to convert
   *
   * @returns A document layout representing the stringified Int8 value
   */
  @unsafe
  provide let int8 = (val: Int8) =>
    Doc.Builder.string(NumberUtils.itoa32(untagInt8(val), defaultRadix))
  /**
   * Converts an Int16 value to its string representation.
   *
   * @param val: The Int16 value to convert
   *
   * @returns A document layout representing the stringified Int16 value
   */
  @unsafe
  provide let int16 = (val: Int16) =>
    Doc.Builder.string(NumberUtils.itoa32(untagInt16(val), defaultRadix))
  /**
   * Converts a Uint8 byte value to its string representation.
   *
   * @param val: The Uint8 value to convert
   *
   * @returns A document layout representing the stringified Uint8 value
   */
  @unsafe
  provide let uint8 = (val: Uint8) =>
    Doc.Builder.string(NumberUtils.itoa32(untagUint8(val), defaultRadix))
  /**
   * Converts a Uint16 value to its string representation.
   *
   * @param val: The Uint16 value to convert
   *
   * @returns A document layout representing the stringified Uint16 value
   */
  @unsafe
  provide let uint16 = (val: Uint16) =>
    Doc.Builder.string(NumberUtils.itoa32(untagUint16(val), defaultRadix))

  // Polymorphic conversions
  let shortValueToString = (val: GrainValue.ShortValue<a>, state) => {
    // Get The Short Tag
    match (GrainValue.getShortTag(val)) {
      CharTag(v) => char(v, state),
      Int8Tag(v) => int8(v),
      Int16Tag(v) => int16(v),
      Uint8Tag(v) => uint8(v),
      Uint16Tag(v) => uint16(v),
      UnknownShortTag => Doc.Builder.string("<unknown short value>"),
    }
  }

  // Constant Values
  @unsafe
  let constantValueToString = (val: GrainValue.ConstantValue<a>) => {
    use WasmI32.{ (==) }
    let v = WasmI32.fromGrain(val)
    if (v == WasmI32.fromGrain(true)) {
      Doc.Builder.string("true")
    } else if (v == WasmI32.fromGrain(false)) {
      Doc.Builder.string("false")
    } else if (v == WasmI32.fromGrain(void)) {
      Doc.Builder.string("void")
    } else {
      Doc.Builder.string("<unknown constant value>")
    }
  }
  @unsafe
  let rec heapValueToString = (val: GrainValue.HeapValue<a>, state) => {
    use Doc.Builder.{ (++) }
    use WasmI32.{ (!=) }
    // Check for Cycles
    if (isCycleInStack(val, state.cycleStack)) {
      let cycleMap = unbox(state.cycleMap)
      let cycleIndex = match (getCycleNumber(val, cycleMap)) {
        -1n => {
          use WasmI32.{ (+) }
          let index = match (cycleMap) {
            [] => 1n,
            [(_, n), ..._] => untagSimpleNumber(n) + 1n,
          }
          state.cycleMap := [(val, tagSimpleNumber(index)), ...cycleMap]
          index
        },
        n => n,
      }
      return Doc.Builder.string("<cycle to <")
        ++ Doc.Builder.string(NumberUtils.itoa32(cycleIndex, defaultRadix))
        ++ Doc.Builder.string(">>")
    }
    // Get The Heap Tag
    let content = match (GrainValue.getHeapTag(val)) {
      TupleTag(v) =>
        tuple(v, { ...state, cycleStack: [val, ...state.cycleStack] }),
      ArrayTag(v) =>
        array(v, { ...state, cycleStack: [val, ...state.cycleStack] }),
      RecordTag(v) =>
        _record(v, { ...state, cycleStack: [val, ...state.cycleStack] }),
      ADTTag(v) => adt(v, { ...state, cycleStack: [val, ...state.cycleStack] }),
      ClosureTag(_) => Doc.Builder.string("<lambda>"),
      StringTag(v) => string(v, state),
      BytesTag(v) => bytes(v),
      // Boxed Number Types
      BoxedNumberTag(v) => number(v),
      // Generic Number Types
      Int32Tag(v) => int32(v),
      Float32Tag(v) => float32(v),
      Uint32Tag(v) => uint32(v),
      Uint64Tag(v) => uint64(v),
      // Unknown
      GrainValue.UnknownTag => Doc.Builder.string("<unknown heap value>"),
    }
    let cycleIndex = getCycleNumber(val, unbox(state.cycleMap))
    return if (cycleIndex != -1n) {
      Doc.Builder.group(
        Doc.Builder.angleBrackets(
          Doc.Builder.string(NumberUtils.itoa32(cycleIndex, defaultRadix))
        )
          ++ Doc.Builder.blank(1)
          ++ content
      )
    } else {
      content
    }
  }
  and tupleHelp = (vals: List<GrainValue.StackTag<a>>, isVariant, state) => {
    let state = { ...state, topLevel: false }
    use Doc.Builder.{ (++) }
    let content = Doc.Builder.parens(
      Doc.Builder.indent(
        Doc.Builder.concatMap(
          lead=next => Doc.Builder._break,
          sep=(prev, next) => Doc.Builder.breakableSpace,
          trail=prev => Doc.Builder.empty,
          func=(final, val) => {
            let val = taggedValue(val, state)
            if (final) {
              Doc.Builder.group(val) ++ Doc.Builder.trailingComma
            } else {
              Doc.Builder.group(val ++ Doc.Builder.comma)
            }
          },
          vals
        )
      )
        ++ Doc.Builder._break
    )
    match (vals) {
      [_] when !isVariant => Doc.Builder.string("box") ++ content,
      _ => content,
    }
  }
  /**
   * Converts a tuple value to its string representation.
   *
   * @param val: The tuple value to convert
   * @param state: The current stringification state
   *
   * @returns A document layout representing the stringified tuple value
   */
  and tuple = (val: GrainValue.TupleValue<e>, state) => {
    let vals = GrainValue.getTupleData(val)
    tupleHelp(vals, false, state)
  }
  /**
   * Converts an array value to its string representation.
   *
   * @param val: The array value to convert
   * @param state: The current stringification state
   *
   * @returns A document layout representing the stringified array value
   */
  and array = (val: Array<a>, state) => {
    let state = { ...state, topLevel: false }
    use Doc.Builder.{ (++) }
    let vals = GrainValue.getArrayData(val)
    Doc.Builder.arrayBrackets(
      Doc.Builder.indent(
        Doc.Builder.concatMap(
          lead=next => Doc.Builder._break,
          sep=(prev, next) => Doc.Builder.breakableSpace,
          trail=prev => Doc.Builder.empty,
          func=(final, val) => {
            let val = taggedValue(val, state)
            if (final) {
              Doc.Builder.group(val) ++ Doc.Builder.trailingComma
            } else {
              Doc.Builder.group(val ++ Doc.Builder.comma)
            }
          },
          vals
        )
      )
        ++ Doc.Builder._break
    )
  }
  and _recordHelp = (vals: List<(String, GrainValue.StackTag<a>)>, state) => {
    use Doc.Builder.{ (++) }
    Doc.Builder.braces(
      Doc.Builder.indent(
        Doc.Builder.concatMap(
          lead=next => Doc.Builder.space ++ Doc.Builder._break,
          sep=(prev, next) => Doc.Builder.breakableSpace,
          trail=last => Doc.Builder.space,
          func=(final, (name, val)) => {
            let val = taggedValue(val, { ...state, topLevel: false })
            let entry = Doc.Builder.group(
              Doc.Builder.string(name)
                ++ Doc.Builder.string(":")
                ++ Doc.Builder.space
                ++ val
            )
            if (final) {
              entry ++ match (vals) {
                [_] => Doc.Builder.comma,
                _ => Doc.Builder.trailingComma,
              }
            } else {
              Doc.Builder.group(entry ++ Doc.Builder.comma)
            }
          },
          vals
        )
      )
        ++ Doc.Builder._break
    )
  }
  /**
   * Converts a record value to its string representation.
   *
   * @param val: The record value to convert
   * @param state: The current stringification state
   *
   * @returns A document layout representing the stringified record value
   */
  and _record = (val: GrainValue.RecordValue<a>, state) => {
    _recordHelp(GrainValue.getRecordData(val), state)
  }
  /**
   * Converts a list value to its string representation.
   *
   * @param val: The list value to convert
   * @param state: The current stringification state
   *
   * @returns A document layout representing the stringified list value
   */
  and list = (val: List<a>, state) => {
    use Doc.Builder.{ (++) }
    Doc.Builder.listBrackets(
      Doc.Builder.indent(
        Doc.Builder.concatMap(
          lead=next => Doc.Builder._break,
          sep=(prev, next) => Doc.Builder.breakableSpace,
          trail=prev => Doc.Builder.empty,
          func=(final, val) => {
            let value = value(val, { ...state, topLevel: false })
            if (final) {
              Doc.Builder.group(value) ++ Doc.Builder.trailingComma
            } else {
              Doc.Builder.group(value ++ Doc.Builder.comma)
            }
          },
          val
        )
      )
        ++ Doc.Builder._break
    )
  }
  /**
   * Converts an ADT value to its string representation.
   *
   * @param val: The ADT value to convert
   * @param state: The current stringification state
   *
   * @returns A document layout representing the stringified ADT value
   */
  and adt = (val: GrainValue.ADTValue<a>, state) => {
    use Doc.Builder.{ (++) }
    if (GrainValue.isListVariant(val)) {
      list(magic(val): List<b>, { ...state, topLevel: false })
    } else {
      let (name, variantData) = GrainValue.getVariantData(val)
      Doc.Builder.string(name) ++ match (variantData) {
        GrainValue.TupleVariant(vals) =>
          tupleHelp(vals, true, { ...state, topLevel: false }),
        GrainValue.RecordVariant(vals) =>
          _recordHelp(vals, { ...state, topLevel: false }),
        EmptyVariant => Doc.Builder.empty,
      }
    }
  }
  and taggedValue = (val: GrainValue.StackTag<a>, state) => {
    match (val) {
      HeapTag(v) => heapValueToString(v, state),
      ShortTag(v) => shortValueToString(v, state),
      NumberTag(v) => number(v),
      ConstantTag(v) => constantValueToString(v),
      UnknownTag => Doc.Builder.string("<unknown value>"),
    }
  }
  /**
   * Converts a Grain value to its string representation.
   *
   * @param value: The grain value to convert
   * @param state: The current stringification state
   *
   * @returns A document layout representing the stringified value
   */
  and value = (value: a, state) => taggedValue(GrainValue.getTag(value), state)

  provide { value, array, list, tuple, _record, adt }
}

/**
 * Converts any Grain value to its string representation.
 *
 * @param val: The Grain value to convert
 *
 * @returns The string representation of the Grain value
 */
provide let toString = (val: a) => {
  // Note: This is a bit of hack to get around grain's lack of forall types
  let val = magic(val): b
  let ast = Stringify.value(val, Stringify.emptyState())
  Doc.Engine.toString(LF, 80, ast)
}
